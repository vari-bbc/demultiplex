import pandas as pd
import numpy as np
import io
#from cStringIO import StringIO
import os
import re
from snakemake.utils import validate, min_version
##### set minimum snakemake version #####
min_version("5.28.0")

# https://stackoverflow.com/questions/10717504/is-it-possible-to-use-read-csv-to-read-only-specific-lines
s = io.StringIO()
read_lens = []
with open('SampleSheet.csv') as f:
    skip_line = True
    reads_section = False
    for line in f:
        # following two if blocks for parsing stuff below [Data]
        if not skip_line:
            s.write(line)
        if line.startswith('[Data]'):
            skip_line = False
        # following code for extracting out the read lengths to determine SE or PE
        if line.startswith('[Reads]'):
            reads_section = True
        elif line.startswith('['):
            reads_section = False
        if reads_section:
            read_len_search = re.search('^(\d+)', line)
            if read_len_search:
                read_lens.append(read_len_search.group(1))
s.seek(0) # "rewind" to the beginning of the StringIO object

# read in the csv with the header lines removed
samples = pd.read_csv(s)


# Next we add a SampleNum column based on order of appearance in the Sample_Name column
sample_names = samples.Sample_Name.values
uniq_samples = pd.unique(sample_names).tolist()

samples['SampleNum'] = [uniq_samples.index(sampleName) for sampleName in sample_names]
samples['SampleNum'] = samples['SampleNum'] + 1

# check that either 1 or 2 read lengths parsed from SampleSheet.csv
assert (0 < len(read_lens) < 3), "Number of read lengths parsed not 1 or 2. It was " + str(len(read_lens))

# whether data is PE (or SE)
paired_end = True if len(read_lens) == 2 else False

if 'Lane' in samples.columns:
    lanes = pd.unique(samples['Lane'])
    sample_fqs = expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/{sample.Sample_Name}_S{sample.SampleNum}_L00{sample.Lane}_R{read}_001.fastq.gz", sample=samples.itertuples(), read=[1,2] if paired_end else [1]),
    undetermined_fqs = expand("Data/Intensities/BaseCalls/Undetermined_S0_L00{lane}_R{read}_001.fastq.gz", read=[1,2] if paired_end else [1], lane=lanes)
else:
    lanes = [1,2,3,4] # if no Lane column in SampleSheet.csv, assume it is NextSeq and that each sample spread over 4 lanes.
    sample_fqs = expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/{sample.Sample_Name}_S{sample.SampleNum}_L00{lane}_R{read}_001.fastq.gz", sample=samples.itertuples(), read=[1,2] if paired_end else [1], lane=lanes),
    undetermined_fqs = expand("Data/Intensities/BaseCalls/Undetermined_S0_L00{lane}_R{read}_001.fastq.gz", read=[1,2] if paired_end else [1], lane=lanes)

#fastqc_and_screen_out = expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/FastQC/{sample.Sample_Name}_L000_R{read}_001_{prog}.html", sample=samples.itertuples(), read=[1,2] if paired_end else [1], prog=['fastqc','screen']) + expand("Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001_{prog}.html", read=[1,2] if paired_end else [1], prog=['fastqc','screen'])

rule all:
    input:
        sample_fqs,
        undetermined_fqs,
        expand("Data/Intensities/BaseCalls/{project}/multiqc_report.html", project=pd.unique(samples['Sample_Project'])) #if "run_qc" in config else [],
        #fastqc_and_screen_out if "run_qc" in config else [],
        #expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/FastQC/{sample.Sample_Name}_L000_R{read}_001_{prog}.html", sample=samples.itertuples(), read=[1,2] if paired_end else [1], prog=['fastqc','screen']),
        #expand("Data/Intensities/BaseCalls/Undetermined_S0_L000_R{read}_001_{prog}.html", read=[1,2] if paired_end else [1], prog=['fastqc','screen']),
        #expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/{sample.Sample_Name}_S{sample.SampleNum}_L00{sample.Lane}_R{read}_001.fastq.gz", sample=samples.itertuples(), read=[1,2] if paired_end else [1])

rule bcl2fastq:
    """
    Run bcl2fastq.
    """
    input:
    output:
        log="bcl2fastq.log",
        done="bcl2fastq.done",
        sample_fqs=sample_fqs,
        undetermined_fqs=undetermined_fqs,
        missing_fastqs_yaml="snakemake_job_logs/missing_fastqs_mqc.yaml",
        missing_fastqs_log="missing_fastqs_mqc.log",
        #sample_fqs=expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/{sample.Sample_Name}_S{sample.SampleNum}_L00{sample.Lane}_R{read}_001.fastq.gz", sample=samples.itertuples(), read=[1,2] if paired_end else [1]),
        #undetermined_fqs=expand("Data/Intensities/BaseCalls/Undetermined_S0_L00{lane}_R{read}_001.fastq.gz", read=[1,2] if paired_end else [1], lane=pd.unique(samples['Lane']))
    params:
    log:
        stdout="snakemake_job_logs/bcl2fastq/out.o",
        stderr="snakemake_job_logs/bcl2fastq/out.e"
    benchmark:
        "snakemake_job_logs/benchmarks/bcl2fastq/benchmark.txt"
    envmodules:
        "bbc/bcl2fastq2/bcl2fastq2-2.20.0"
    threads: 8
    resources:
        mem_gb = 180
    shell:
        """
        bcl2fastq &> {output.log}

        echo "id: 'missing-fqs'\nsection_name: 'Empty Fastq files after dmux'\ndescription: 'This section lists all the fastq files that ended up having 0 reads after bcl2fastq.'\nplot_type: 'html'\ndata: |\n    <ul>" > {output.missing_fastqs_yaml}
        
        for fastq in {output.sample_fqs}
        do
            if [[ ! -f $fastq ]]
            then
            echo "      <li>`basename $fastq`</li>" >> {output.missing_fastqs_yaml}
            echo $fastq >> {output.missing_fastqs_log}
            touch $fastq

            fi
        done
        echo "    </ul>" >> {output.missing_fastqs_yaml}

        touch {output.done}
        """

def get_all_lanes_fqs(wildcards):
    if 'Lane' in samples.columns:
        fqs = expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/{sample.Sample_Name}_S{sample.SampleNum}_L00{sample.Lane}_R{read}_001.fastq.gz", sample=samples[samples['Sample_Name'] == wildcards.sample].itertuples(), read=wildcards.read)
    else:
        fqs = expand("Data/Intensities/BaseCalls/{sample.Sample_Project}/{sample.Sample_Name}_S{sample.SampleNum}_L00{lane}_R{read}_001.fastq.gz", sample=samples[samples['Sample_Name'] == wildcards.sample].itertuples(), read=wildcards.read, lane=lanes)

    return fqs

rule cat_fqs:
    """
    Merge lanes for sample fastq.gz files.
    """
    input:
        get_all_lanes_fqs
        
    output:
        "Data/Intensities/BaseCalls/{project}/{sample}_L000_R{read}_001.fastq.gz"
    params:
    log:
        stdout="snakemake_job_logs/cat_fqs/{project}/{sample}_R{read}.o",
        stderr="snakemake_job_logs/cat_fqs/{project}/{sample}_R{read}.e"
    benchmark:
        "snakemake_job_logs/benchmarks/cat_fqs/{project}/{sample}_R{read}.txt"
    envmodules:
    threads: 1
    resources:
        mem_gb = 22
    run:
        if len(input) > 1:
            shell("cat {input} > {output}")
        else:
            shell("ln -sr {input} {output}")

rule cat_fqs_undetermined:
    """
    Merge lanes for undetermined fastq.gz files.
    """
    input:
        expand("Data/Intensities/BaseCalls/Undetermined_S0_L00{lane}_R{{read}}_001.fastq.gz", lane=lanes)
    output:
        "Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001.fastq.gz"
    params:
    log:
        stdout="snakemake_job_logs/cat_fqs_undetermined/Undetermined_L000_R{read}.o",
        stderr="snakemake_job_logs/cat_fqs_undetermined/Undetermined_L000_R{read}.e"
    benchmark:
        "snakemake_job_logs/benchmarks/cat_fqs_undetermined/Undetermined_L000_R{read}.txt"
    envmodules:
    threads: 1
    resources:
        mem_gb = 22
    run:
        if len(input) > 1:
            shell("cat {input} > {output}")
        else:
            shell("ln -sr {input} {output}")

rule symlink_undet_to_each_proj:
    """
    Symlink Undetermined files to project directories.
    """
    input:
        expand("Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001{suffix}", read=[1,2] if paired_end else [1], suffix=['_fastqc.html','_screen.html','_fastqc.zip','_screen.txt','.fastq.gz']),
        expand("Data/Intensities/BaseCalls/Undetermined_S0_L00{lane}_R{read}_001.fastq.gz", read=[1,2] if paired_end else [1], lane=lanes),
    output:
        expand("Data/Intensities/BaseCalls/{{project}}/Undetermined_L000_R{read}_001{suffix}", read=[1,2] if paired_end else [1], suffix=['_fastqc.html','_screen.html','_fastqc.zip','_screen.txt','.fastq.gz']),
        expand("Data/Intensities/BaseCalls/{{project}}/Undetermined_S0_L00{lane}_R{read}_001.fastq.gz", read=[1,2] if paired_end else [1], lane=lanes),
    log:
        stdout="snakemake_job_logs/symlink_undet_to_each_proj/{project}.o",
        stderr="snakemake_job_logs/symlink_undet_to_each_proj/{project}.e"
    benchmark:
        "snakemake_job_logs/benchmarks/symlink_undet_to_each_proj/{project}.txt"
    params:
        project = "{project}"
    threads: 1
    resources:
        mem_gb=32
    envmodules:
    shell:
        """
        ln -sr Data/Intensities/BaseCalls/Undetermined* Data/Intensities/BaseCalls/{params.project}

        """


rule multiqc:
    """
    Make multiQC report.
    """
    input:
        lambda wildcards: expand("Data/Intensities/BaseCalls/{{project}}/FastQC/{sample.Sample_Name}_L000_R{read}_001_{prog}.html", sample=samples[samples['Sample_Project'] == wildcards.project].itertuples(), read=[1,2] if paired_end else [1], prog=['fastqc','screen']),
        expand("Data/Intensities/BaseCalls/{{project}}/Undetermined_L000_R{read}_001_{prog}.html", read=[1,2] if paired_end else [1], prog=['fastqc','screen']),
    output:
        "Data/Intensities/BaseCalls/{project}/multiqc_report.html",
        directory("Data/Intensities/BaseCalls/{project}/multiqc_data")
    log:
        stdout="snakemake_job_logs/multiqc/{project}.o",
        stderr="snakemake_job_logs/multiqc/{project}.e",
    benchmark:
        "snakemake_job_logs/benchmarks/multiqc/{project}.txt"
    params:
        project_dir="Data/Intensities/BaseCalls/{project}/",
        missing_fqs_log="snakemake_job_logs/"
    threads: 1
    resources:
        mem_gb=32
    envmodules:
        "bbc/multiqc/multiqc-1.9"
    shell:
        """
        multiqc -f \
        -o {params.project_dir} {params.project_dir} {params.missing_fqs_log}

        """


rule fastqc:
    """
    Run fastqc for merged (across lanes) sample files.
    """
    input:
        "Data/Intensities/BaseCalls/{project}/{sample}_L000_R{read}_001.fastq.gz"
    output:
        html="Data/Intensities/BaseCalls/{project}/FastQC/{sample}_L000_R{read}_001_fastqc.html",
        zip="Data/Intensities/BaseCalls/{project}/FastQC/{sample}_L000_R{read}_001_fastqc.zip"
    params:
        outdir="Data/Intensities/BaseCalls/{project}/FastQC/"
    log:
        stdout="snakemake_job_logs/fastqc/{project}/{sample}_R{read}.o",
        stderr="snakemake_job_logs/fastqc/{project}/{sample}_R{read}.e"
    benchmark:
        "snakemake_job_logs/benchmarks/fastqc/{project}/{sample}_R{read}.txt"
    envmodules:
        "bbc/fastqc/fastqc-0.11.9"
    threads: 1
    resources:
        mem_gb = 22
    shell:
        """
        if [ -s {input} ]
        then
            fastqc --outdir {params.outdir} {input}
        else
            touch {output.html} {output.zip}
        fi
        """

rule fastqc_undetermined:
    """
    Run fastqc for merged (across lanes) undetermined files.
    """
    input:
        "Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001.fastq.gz"
    output:
        html="Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001_fastqc.html",
        zip="Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001_fastqc.zip"
    params:
        outdir="Data/Intensities/BaseCalls/"
    log:
        stdout="snakemake_job_logs/fastqc/Undetermined_L000_R{read}.o",
        stderr="snakemake_job_logs/fastqc/Undetermined_L000_R{read}.e"
    benchmark:
        "snakemake_job_logs/benchmarks/fastqc/Undetermined_L000_R{read}.txt"
    envmodules:
        "bbc/fastqc/fastqc-0.11.9"
    threads: 1
    resources:
        mem_gb = 22
    shell:
        """
        fastqc --outdir {params.outdir} {input}
        """

rule fastq_screen:
    """
    Run fastq_screen for merged (across lanes) sample files.
    """
    input:
        "Data/Intensities/BaseCalls/{project}/{sample}_L000_R{read}_001.fastq.gz"
    output:
        html = "Data/Intensities/BaseCalls/{project}/FastQC/{sample}_L000_R{read}_001_screen.html",
        txt = "Data/Intensities/BaseCalls/{project}/FastQC/{sample}_L000_R{read}_001_screen.txt",
    params:
        outdir = "Data/Intensities/BaseCalls/{project}/FastQC/"
    log:
        stdout="snakemake_job_logs/fastq_screen/{project}/{sample}_R{read}.o",
        stderr="snakemake_job_logs/fastq_screen/{project}/{sample}_R{read}.e"
    benchmark:
        "snakemake_job_logs/benchmarks/fastq_screen/{project}/{sample}_R{read}.txt"
    envmodules:
        "bbc/fastq_screen/fastq_screen-0.14.0"
    threads: 4
    resources:
        mem_gb = 88
    shell:
        """
        if [ -s {input} ]
        then
            fastq_screen --threads {threads} --outdir {params.outdir} {input}
        else
            touch {output.html} {output.txt}
        fi
        """

rule fastq_screen_undetermined:
    """
    Run fastq_screen for merged (across lanes) sample files.
    """
    input:
        "Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001.fastq.gz"
    output:
        html = "Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001_screen.html",
        txt = "Data/Intensities/BaseCalls/Undetermined_L000_R{read}_001_screen.txt",
    params:
        outdir = "Data/Intensities/BaseCalls/"
    log:
        stdout="snakemake_job_logs/fastq_screen/Undetermined_L000_R{read}.o",
        stderr="snakemake_job_logs/fastq_screen/Undetermined_L000_R{read}.e"
    benchmark:
        "snakemake_job_logs/benchmarks/fastq_screen/Undetermined_L000_R{read}.txt"
    envmodules:
        "bbc/fastq_screen/fastq_screen-0.14.0"
    threads: 4
    resources:
        mem_gb = 88
    shell:
        """
        fastq_screen --threads {threads} --outdir {params.outdir} {input}
        """


